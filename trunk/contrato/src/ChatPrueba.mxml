<?xml version="1.0" encoding="utf-8"?>
<mx:Application xmlns:mx="http://www.adobe.com/2006/mxml" layout="absolute" applicationComplete="connect()">
	<mx:Script>
		<![CDATA[
			import mx.collections.ArrayCollection; 
			import flash.net.*;
			import flash.events.*;
			import flash.utils.*;
			import mx.controls.*;
			import mx.core.UIComponent;
									
			private const SERVER:String = "rtmfp://p2p.rtmfp.net/";
			private const DEVKEY:String = "c4563db27753834c53fb2484-d63f32241b81";
			
			private var nc:NetConnection;
			private var netGroup:NetGroup;
			
			[Bindable]
			private var user:String;
			
			[Bindable]
			private var connected:Boolean = false;
			
			private function connect():void{
				nc = new NetConnection();
				nc.addEventListener(NetStatusEvent.NET_STATUS,netStatus);
				nc.connect(SERVER+DEVKEY);	
			}
			
			private function netStatus(event:NetStatusEvent):void{
				write(event.info.code);
				
				switch(event.info.code){
					case "NetConnection.Connect.Success":
						setupGroup()
						break;
					
					case "NetGroup.Connect.Success":
						connected = true;
						
						break;
					
					case "NetGroup.Posting.Notify":
						receiveMessage(event.info.message);
						break;
				}
			}
			
			private function setupGroup():void{
				var groupspec:GroupSpecifier = new GroupSpecifier("myGroup/g1");
				groupspec.serverChannelEnabled = true;
				groupspec.postingEnabled = true;
				
				trace("Groupspec: "+groupspec.groupspecWithAuthorizations());
				
				netGroup = new NetGroup(nc,groupspec.groupspecWithAuthorizations());
				netGroup.addEventListener(NetStatusEvent.NET_STATUS,netStatus);
			
				
				user = "user"+Math.round(Math.random()*10000);
			}
			
			private var sequence:uint = 0;
			
			private function sendMessage():void{
				
				var message:Object = new Object();
				message.sender = netGroup.convertPeerIDToGroupAddress(nc.nearID);
				//message.sequence = sequence++;
				message.user = txtUser.text;
				message.text = txtMessage.text;
				
				
				netGroup.post(message);
				receiveMessage(message);
				
				txtMessage.text = "";
			}
			
			private function receiveMessage(message:Object):void{
				write(message.user+": "+message.text);
			}
			
			private function write(txt:String):void{
				txtHistory.text += txt+"\n";
			}

		]]>
	</mx:Script>
	<mx:TextArea left="10" right="10" top="10" bottom="40" id="txtHistory" />
	<mx:TextInput x="10" id="txtUser" text="{user}" bottom="10" width="127"/>
	<mx:TextInput left="145" right="88" id="txtMessage" bottom="10" enter="sendMessage()"/>
	<mx:Button label="Enviar" click="sendMessage()" enabled="{connected}" bottom="10" right="10"/>
</mx:Application>
